//===== rAthena Script =======================================
//= Prontera Culvert Instance (kRo Zero)
//============================================================

//Mapflag
1@prtc	mapflag	nomemo
1@prtc	mapflag	nosave	SavePoint
1@prtc	mapflag	nowarp
1@prtc	mapflag	nowarpto
1@prtc	mapflag	noreturn
1@prtc	mapflag	nobranch
1@prtc	mapflag	noicewall
1@prtc	mapflag	partylock
1@prtc	mapflag	noteleport
1@prtc	mapflag	monster_noteleport


// Warps
1@prtc,19,175,0	warp2	#prtc1	4,2,1@prtc,60,28
1@prtc,60,24,0	warp2	#prtc2	4,2,1@prtc,19,171
1@prtc,100,176,0	warp2	#prtc3	4,2,1@prtc,140,28
1@prtc,140,24,0	warp2	#prtc4	4,2,1@prtc,100,172

//Instance Timer (custom)
1@prtc,3,1,5	script	#prtc_timer	CLEAR_NPC,{
	end;
OnStartTimer:
	initnpctimer;
	end;
OnStopTimer:
	//callfunc("F_InstanceTimer","pc",getnpctimer(0),'Mode);
	//callfunc("F_UpdateInsRecord");
	end;
OnTimer3600000:
	stopnpctimer;
	end;
OnInstanceInit:
	hideonnpc instance_npcname("#prtc_timer");
	end;
}

//NPC
prt_fild05,281,227,5	script	Manager Jurgens#prtc	853,{

	.@party_id = getcharid(1);
	.@ins_mas = getpartyleader(.@party_id,2);
	.@p_name$ = getpartyname(.@party_id);
	.@md_name$ = "Prontera Culvert";
	
	mes "["+strnpcinfo(1)+"]";
	mes "Hello there.";
	if(Sex == 1) set .@sex$,"Boy";
		else set .@sex$,"Girl";
	mes .@sex$+", come closer.";
	next;
	mes "["+strnpcinfo(1)+"]";
	mes "I'm the manager of the ^FF0000Prontera Culvert^000000, and I'm in trouble.";
	next;
	mes "["+strnpcinfo(1)+"]";
	mes "The culverts have been infested with all kings of bugs...";
	next;
	mes "["+strnpcinfo(1)+"]";
	mes "This problem will affect the proper functioning of the culverts if left untreated.";
	next;
	mes "["+strnpcinfo(1)+"]";
	mes "I've been looking for adventurers who can assist me...";
	next;
	mes "["+strnpcinfo(1)+"]";
	mes "Could you...";
	mes "..help me?";
	next;
	if(getcharid(0) == .@ins_mas) .@str$ = "I'll clean the ^FF0000Prontera Culvert^000000 for you!";
	
	switch(select(.@str$,"Enter ^FF0000Prontera Culvert^000000","Cancel")) {
	case 1:
	
		//Load Config Settings
		.@select = select(getvariableofnpc(.menu_config$,"#Instance_Config"));
		.@arr$ = getvariableofnpc(.level_config$,"#Instance_Config");
		explode(.@config$,.@arr$,"-");
		for(.@i = 0; .@i<getarraysize(.@config$); .@i+=11) {
			if(.@config$[.@i+1] == .@md_name$) {
				.@quest = atoi(.@config$[.@i+4 + 3*(.@select-1)]);
				.@min_level = atoi(.@config$[.@i+2 + 3*(.@select-1)]);
				.@max_level = atoi(.@config$[.@i+3 + 3*(.@select-1)]);
				break;
			}
		}
		//Check Cooldown
		callfunc("F_ChkInsCD",.@quest);	
		//Check Level
		callfunc("F_ChkInsLv",.@min_level,.@max_level);
		
		//Create Instance
		.@inst_id = instance_create(.@md_name$);
		if (.@inst_id < 0) {
			mes "Party Name: "+ getpartyname(.@party_id);
			mes "Party Leader: "+strcharinfo(0);
			mes "^0000ff"+.@md_name$+" ^000000 - Reservation Failed.";
			close;
		}

		setinstancevar('Mode,.@select,.@inst_id);
		
		mes "["+strnpcinfo(1)+"]";
		mes "You may now enter.";
		//debugmes "Instance Creation: Prontera Culvert";
		next;
		if(select("Enter ^FF0000Prontera Culvert^000000","Wait a while")==2) close;
	case 2:
		if(instance_id() != 0) {
			//Load Config Settings
			.@arr$ = getvariableofnpc(.level_config$,"#Instance_Config");
			explode(.@config$,.@arr$,"-");
			for(.@i = 0; .@i<getarraysize(.@config$); .@i+=11) {
				if(.@config$[.@i+1] == .@md_name$) {
					.@quest = atoi(.@config$[.@i+4 + 3*('Mode-1)]);
					.@min_level = atoi(.@config$[.@i+2 + 3*('Mode-1)]);
					.@max_level = atoi(.@config$[.@i+3 + 3*('Mode-1)]);
					break;
				}
			}
			//Check Cooldown
			callfunc("F_ChkInsCD",.@quest);	
			//Check Level
			callfunc("F_ChkInsLv",.@min_level,.@max_level);
		}
		
		mes "["+strnpcinfo(1)+"]";
		switch( instance_enter(.@md_name$) ) {
		case IE_OTHER:
			mes "An unknown error has occurred.";
			close;
		case IE_NOINSTANCE:
			mes "The memorial dungeon "+.@md_name$+" does not exist.";
			mes "The party leader did not generate the dungeon yet.";
			close;
		case IE_NOMEMBER:
			mes "Only the registered members can enter the instance "+.@md_name$+".";
			close;
		case IE_OK:
			if(.@quest){
				setquest .@quest;
			break;
			}
		}

		//debugmes "Instance Entrance Prontera Culvert "+strcharinfo(0)+".";
		close;
	case 3: close;
	}
	
OnInit:
	waitingroom "Culvert Clearing",0;
	end;
}

1@prtc,24,19,3	script	Manager Jurgens#prtc2	853,9,9,{
	if(getcharid(0) != getpartyleader(getcharid(1),2)) {
		mes "["+strnpcinfo(1)+"]";
		mes "I wish to talk to your Party Leader.";
		close;
	}
	'party_id = getcharid(1);
	mes "["+strnpcinfo(1)+"]";
	mes "Thief bugs have swarmed the area and contaminated the water supply.";
	npctalk "Thief bugs have swarmed the area and contaminated the water supply.";
	next;
	mes "["+strnpcinfo(1)+"]";
	mes "The main source of the thief bugs are the eggs.";
	mes "Kill the thief bug eggs.";
	npctalk "The main source of the thief bus are the eggs. Kill the thief bug eggs.";
	next;
	mes "["+strnpcinfo(1)+"]";
	mes "They would eventually cause trouble if you don't get rid of them in time...";
	npctalk "They would eventually cause trouble if you don't get rid of them in time...";
	next;
	mes "["+strnpcinfo(1)+"]";
	mes "Thank you for helping out!";
	npctalk "Thank you for helping out!";
	close2;
	hideonnpc instance_npcname(strnpcinfo(0));
	donpcevent instance_npcname("Manager Jurgens#prtc2") + "::OnStart"; 
	donpcevent instance_npcname("#prtc_timer") + "::OnStartTimer"; 
	end;

OnInstanceInit:
	'Mode = 0;
	'Boss = 0;
	'BossActive = 0;
	'EggKilled = 0;
	'Buff = 0;
	'Map$ = instance_mapname("1@prtc");
	end;
OnTouch_:
//	npctalk "Welcome to Prontera Culverts";
	end;
	
OnStart:
	//Retrieve Mode Modifiers
	.@arr$ = getvariableofnpc(.mode_config$,"#Instance_Config");
	explode(.@mod$,.@arr$,"-");
	for(.@i=0; .@i<getarraysize(.@mod$); .@i+=10) {
		if(atoi(.@mod$[.@i]) == 'Mode) {
			.@hp_mod = atoi(.@mod$[.@i+1]);
			.@atk_mod = atoi(.@mod$[.@i+2]);
			.@amt_mod = atoi(.@mod$[.@i+3]);
			'boss_hp_mod  = atoi(.@mod$[.@i+4]);
			'boss_atk_mod  = atoi(.@mod$[.@i+5]);
			'boss_def_mod  = atoi(.@mod$[.@i+6]);
			'boss_mdef_mod = atoi(.@mod$[.@i+7]);
			'boss_hit_mod  = atoi(.@mod$[.@i+8]);
			'boss_flee_mod = atoi(.@mod$[.@i+9]);
			break;
		}
	}
	
	//Spawn Dynamic Mobs
	setarray .@m_info,3972,25,3973,10;
	for(.@m=0; .@m<getarraysize(.@m_info); .@m+=2) {	
		
		//Initialize Array
		deletearray $@mobid[0],getarraysize($@mobid);

		.@mob$ = .@m_info[.@m]+"_";
		.@amt = .@m_info[.@m+1] * .@amt_mod;
		areamonster 'Map$,15,38,158,175,"--ja--",.@m_info[.@m],.@amt;

		for(.@i = 0; .@i < getarraysize($@mobid); .@i++) {
			setd(".@"+.@mob$+.@i), $@mobid[.@i];
			if(unitexists(getd(".@"+.@mob$+.@i))) {
				getunitdata getd(".@"+.@mob$+.@i),.@data;
				setunitdata getd(".@"+.@mob$+.@i),UMOB_MAXHP,.@data[UMOB_MAXHP] * .@hp_mod;
				setunitdata getd(".@"+.@mob$+.@i),UMOB_HP,.@data[UMOB_HP] * .@hp_mod;		
				setunitdata getd(".@"+.@mob$+.@i),UMOB_ATKMIN,.@data[UMOB_ATKMIN] * .@atk_mod;
				setunitdata getd(".@"+.@mob$+.@i),UMOB_ATKMAX,.@data[UMOB_ATKMAX] * .@atk_mod;
			}
		}
	}
	
	//Eggs
	.@event$ = instance_npcname("Manager Jurgens#prtc2") + "::OnCulvertEggKill";
	sleep 500;
	areamonster 'Map$,15,38,102,170,"--ja--",3974,10,.@event$;	//Thief Bug Egg
	areamonster 'Map$,137,29,182,181,"--ja--",3974,12,.@event$;	//Thief Bug Egg
	
	//Normal Mobs
	areamonster 'Map$,15,38,158,175,"--ja--",1005,5;	//Familiar
	areamonster 'Map$,15,38,158,175,"--ja--",1054,5;	//Thief Bug
	areamonster 'Map$,15,38,158,175,"--ja--",1175,20;	//Tarou
	areamonster 'Map$,15,38,158,175,"--ja--",1014,10;	//Spore
	'BossActive = 0;
	end;

OnCulvertEggKill:
	'EggKilled++;
	.@ct = 22;
	if('EggKilled < .@ct) {
		instance_announce 0,"Remaining Eggs : "+(22-'EggKilled)+"",bc_map,"0xFFFF00",FW_BOLD;
		end;
	}
	if('EggKilled == .@ct && !'BossActive) {
		hideonnpc instance_npcname(strnpcinfo(0));
		instance_announce 0,"Ancient Golden Bug: There is no escape from the deepest abyss of the culverts!!",bc_map,"0xFFFFFF",FW_BOLD;	
		
		//Initialize Array
		deletearray $@mobid[0],getarraysize($@mobid);
		
		if('Boss == 0) monster instance_mapname("1@prtc"),99,99,"--ja--",3975,1,instance_npcname("Manager Jurgens#prtc2") + "::OnBossDead";
		'Boss = $@mobid[0];
		'BossActive = 1;
		
		if(unitexists('Boss)) {
			//Apply Modifiers
			getunitdata 'Boss,.@boss_data;
			'boss_hp = .@boss_data[UMOB_MAXHP] * 'boss_hp_mod;
			'boss_min_atk = .@boss_data[UMOB_ATKMIN] * 'boss_atk_mod;;
			'boss_max_atk = .@boss_data[UMOB_ATKMAX] * 'boss_atk_mod;
			'boss_min_matk = .@boss_data[UMOB_MATKMIN] * 'boss_atk_mod;
			'boss_max_matk = .@boss_data[UMOB_MATKMAX] * 'boss_atk_mod;
			'boss_def = .@boss_data[UMOB_DEF] * 'boss_def_mod;
			'boss_mdef = .@boss_data[UMOB_MDEF] * 'boss_mdef_mod;
			'boss_hit = .@boss_data[UMOB_HIT] * 'boss_hit_mod;
			'boss_flee = .@boss_data[UMOB_FLEE] * 'boss_flee_mod;

			//Set boss data
			setunitdata 'Boss,UMOB_MAXHP,'boss_hp;
			setunitdata 'Boss,UMOB_HP,'boss_hp;
			setunitdata 'Boss,UMOB_ATKMIN,'boss_min_atk;
			setunitdata 'Boss,UMOB_ATKMAX,'boss_max_atk;
			setunitdata 'Boss,UMOB_MATKMIN,'boss_min_matk;
			setunitdata 'Boss,UMOB_MATKMAX,'boss_max_matk;
			setunitdata 'Boss,UMOB_DEF,'boss_def;
			setunitdata 'Boss,UMOB_MDEF,'boss_mdef;
			setunitdata 'Boss,UMOB_HIT,'boss_hit ;
			setunitdata 'Boss,UMOB_FLEE,'boss_flee;

			
			.@label$ = instance_npcname("Manager Jurgens#prtc2") + "::OnEgg";
			.@boss_x = .@boss_data[UMOB_X];
			.@boss_y = .@boss_data[UMOB_Y];
		
			for(.@i = 1; .@i <=1; .@i++) {
				//freeloop(1);
				do {
					.@egg_x = .@boss_x + F_Rand(-3,-2,-1,1,2,3);
					.@egg_y = .@boss_y + F_Rand(-3,-2,-1,1,2,3);
				} while (!checkcell('Map$,.@egg_x,.@egg_y,cell_chkpass) || (.@egg_x == .@x_1 && .@egg_y == .@y_1) || (.@egg_x == .@x_2 && .@egg_y == .@y_2) || (.@egg_x == .@x_3 && .@egg_y == .@y_3) || (.@egg_x == .@x_4 && .@egg_y == .@y_4) );
				//freeloop(0);
				
				switch(.@i){
				case 1: .@x_1 = .@egg_x; .@y_1 = .@egg_y; break;
				case 2: .@x_2 = .@egg_x; .@y_2 = .@egg_y; break;
				case 3: .@x_3 = .@egg_x; .@y_3 = .@egg_y; break;
				case 4: .@x_4 = .@egg_x; .@y_4 = .@egg_y; break;
				default: break;
				}
					
				monster 'Map$,.@egg_x,.@egg_y,"",3974,1, .@label$;
			}
		}
		
		initnpctimer instance_npcname("#Venom_Fog");
		initnpctimer;
	}
	end;
	
OnEgg:
	'Eggs = mobcount(instance_mapname('Map$[0]),instance_npcname(strnpcinfo(0))+"::OnEgg");

	if(unitexists('Boss)) {
		getunitdata 'Boss, .@arr;
		if('Eggs == 0 && 'Buff > 0) {
			setunitdata 'Boss,UMOB_ATKMIN, 'boss_min_atk;
			setunitdata 'Boss,UMOB_ATKMAX, 'boss_max_atk;
			setunitdata 'Boss,UMOB_MATKMIN, 'boss_min_matk;
			setunitdata 'Boss,UMOB_MATKMAX, 'boss_max_matk;
			setunitdata 'Boss,UMOB_DEF, 'boss_def;
			setunitdata 'Boss,UMOB_MDEF, 'boss_mdef;
			setunitdata 'Boss,UMOB_HIT, 'boss_hit;
			setunitdata 'Boss,UMOB_FLEE, 'boss_flee;
			'Buff = 0;
		}
	}
	end;
OnTimer10000:
	if(unitexists('Boss)) {
		getunitdata 'Boss, .@arr;
		.@label$ = instance_npcname("Manager Jurgens#prtc2") + "::OnEgg";
		.@boss_x = .@arr[UMOB_X];
		.@boss_y = .@arr[UMOB_Y];

		//Summon Eggs
		freeloop(1);
		do {
			.@egg_x = .@boss_x + F_Rand(-3,-2,2,3);
			.@egg_y = .@boss_y + F_Rand(-3,-2,2,3);
		} while (!checkcell('Map$[0],.@egg_x,.@egg_y,cell_chkpass));
		freeloop(0);
		
		monster 'Map$[0],.@egg_x,.@egg_y,"",3974,1, .@label$;
		'Slave_Egg = $@mobid[0];
		getunitdata 'Slave_Egg,.@data;
		setunitdata 'Slave_Egg,UMOB_HP,3;
	}
	end;
	
OnTimer15000:
	if(!'BossActive) {
		stopnpctimer;
		end;
	}
	'Eggs = mobcount(instance_mapname('Map$[0]),instance_npcname(strnpcinfo(0))+"::OnEgg");
	if('Eggs >= 1) {
	
		'Heal = 'Eggs;
		.@label$ = instance_npcname("Manager Jurgens#prtc2") + "::OnEgg";
		if(unitexists('Boss)) {
			getunitdata 'Boss, .@arr;

			//Heal
			setunitdata 'Boss,UMOB_HP,.@arr[UMOB_HP] + ('Heal * 5102);
			instance_announce 0,"The Golden Thief Bug has absorbed the nearby eggs and recovered "+('Heal * 5102)+" HP in total.",bc_map,"0xFFFFFF",FW_BOLD;	
			//Buff
			if('Buff < 10) {
				'Buff++;
				setunitdata 'Boss,UMOB_ATKMIN, 'boss_min_atk + (50 * 'Buff);
				setunitdata 'Boss,UMOB_ATKMAX, 'boss_max_atk + (50 * 'Buff);
				setunitdata 'Boss,UMOB_MATKMIN, 'boss_min_matk + (50 * 'Buff);
				setunitdata 'Boss,UMOB_MATKMAX, 'boss_max_matk + (50 * 'Buff);
				setunitdata 'Boss,UMOB_DEF, 'boss_def + 2;
				setunitdata 'Boss,UMOB_MDEF, 'boss_mdef + 2;
				setunitdata 'Boss,UMOB_HIT, 'boss_hit + 2;
				setunitdata 'Boss,UMOB_FLEE, 'boss_flee + 2;
			}
			movenpc instance_npcname(strnpcinfo(0)),.@arr[UMOB_X],.@arr[UMOB_Y];
			specialeffect EF_HEAL,AREA,instance_npcname(strnpcinfo(0));
		}
	}
	initnpctimer;
	end;

OnBossDead:
	stopnpctimer;
	'BossActive = 0;
	donpcevent instance_npcname("#prtc_timer") + "::OnStopTimer"; 
	.@label$ = instance_npcname("Manager Jurgens#prtc2") + "::OnEgg";
	killmonster 'Map$[0], .@label$; 
	donpcevent instance_npcname("#Venom_Fog")+"::OnEnd";
	enablenpc instance_npcname("Treasure Chest#prtc1");
	if ('Mode == 1) {
		enablenpc instance_npcname("Treasure Chest#prtc1");
	} else {
		'unique_members = min(MAX_PARTY, callfunc("F_GetUniquePartyMemberCountInMap", getcharid(1), 'Map$)); // Cap to MAX_PARTY
		'RewardUniqueIDLogs$ = "";
		for (.@i = 1; .@i <= 'unique_members; ++.@i) {
			enablenpc instance_npcname("Treasure Chest#prtc" + .@i);
		}
	}	
	end;
}

1@prtc,1,1,3	script	#Venom_Fog	-1,{
end;

OnTimer50:
	if(!'BossActive || !unitexists('Boss)) end;
	getunitdata 'Boss, .@arr;
	.@label$ = instance_npcname("Manager Jurgens#prtc2") + "::OnVenom";
	.@mobx = .@arr[UMOB_X];
	.@moby = .@arr[UMOB_Y];

	.@x1 = 0;
	.@y1 = -8;
	.@x2 = 0;
	.@y2 = 8;

	monster 'Map$[0],.@mobx+.@x1,.@moby+.@y1,"#toxicfumes",2536,1,.@label$;	//Toxic Fumes
	monster 'Map$[0],.@mobx+.@x2,.@moby+.@y2,"#toxicfumes",2536,1,.@label$;	//Toxic Fumes
	monster 'Map$[0],.@mobx+.@x1+1,.@moby+.@y1-1,"#toxicfumes",2536,1,.@label$;	//Toxic Fumes
	monster 'Map$[0],.@mobx+.@x2-1,.@moby+.@y2+1,"#toxicfumes",2536,1,.@label$;	//Toxic Fumes
	end;
OnEnd:
OnTimer3500:
	.@label$ = instance_npcname("Manager Jurgens#prtc2") + "::OnVenom";
	killmonster 'Map$[0], .@label$; 
	initnpctimer;
	end;
OnFumes:
	end;
}

1@prtc,99,99,4	script	Treasure Chest#prtc1	10005,{
	.@unique_id = callfunc("F_GetUniqueID", getcharid(3));
	if ('Mode >= 2) {
		if (strpos('RewardUniqueIDLogs$, ""+.@unique_id) != -1) {
			end;
		}
		'RewardUniqueIDLogs$ = 'RewardUniqueIDLogs$ + "||" + .@unique_id;
	}

	//Log Event
	//debugmes "Instance Completion: Prontera Culvert"+strcharinfo(0)+".";
	
	set .@ins_map$,strcharinfo(3);
	getpartymember(getcharid(1)),2;
	for( .@i = 0; .@i < $@partymembercount; .@i++ ) {
		if (attachrid($@partymemberaid[.@i])) {
			if (strcharinfo(3) == .@ins_map$) {
				.@party_inmap++;
			}
			detachrid;
		}
	}
	if(.@party_inmap >=7 && .@party_inmap <12) .@bonus = 10;
		else if (.@party_inmap >=12) .@bonus = 15;
		else .@bonus = 0;
	
	if('Mode <= 2) {
		setarray .@reward[1],
		25424,100,	//Shimmering_Crystal
		25424,100,	//Shimmering_Crystal
		25424,100,	//Shimmering_Crystal
		18005,50,	//Mixed_Rune_Fragment
		29587,50,	//Shimmering_Crystal_Box
		29587,25,	//Shimmering_Crystal_Box
		29584,1,	//Rune_Box
		29585,3,	//PVM_Box
		29586,10,	//Refine_Box
		29586,5,	//Refine_Box
		22150,100,	//Expedition_Greaves_4
		22150,50,	//Expedition_Greaves_4
		22150,50,	//Expedition_Greaves_4
		28539,100,	//Expedition_Ring_4
		28539,50,	//Expedition_Ring_4
		28539,50,	//Expedition_Ring_4
		6635,1;		//Blacksmith Blessing
	} else if ('Mode == 3) {
		setarray .@reward[1],
		//25458,100,	//Sparkling_Opal
		//25458,50,	//Sparkling_Opal
		//25458,10,	//Sparkling_Opal
		25424,100,	//Shimmering_Crystal
		25424,100,	//Shimmering_Crystal
		25424,100,	//Shimmering_Crystal
		25424,100,	//Shimmering_Crystal
		25424,100,	//Shimmering_Crystal
		25424,100,	//Shimmering_Crystal
		29584,5,	//Rune_Box
		29586,20,	//Refine_Box
		29585,20,	//Pvm_Box
		29584,1,	//Rune_Box
		29585,2,	//PVM_Box
		29586,10,	//Refine_Box
		18005,100,	//Mixed_Rune_Fragment
		29587,100,	//Shimmering_Crystal_Box
		29587,100,	//Shimmering_Crystal_Box
		756,100,	//Oridecon_Stone
		756,100,	//Oridecon_Stone
		756,30,		//Oridecon_Stone
		756,30,		//Oridecon_Stone
		756,20,		//Oridecon_Stone
		756,20,		//Oridecon_Stone
		757,100,	//Elunium_Stone
		757,100,	//Elunium_Stone
		757,30,		//Elunium_Stone
		757,30,		//Elunium_Stone
		757,20,		//Elunium_Stone
		757,20,		//Elunium_Stone
		984,100,	//Oridecon
		984,15,		//Oridecon
		984,15,		//Oridecon
		985,100,	//Elunium
		985,15,		//Elunium
		985,15,		//Elunium
		//25429,100,	//Mythril_Ore
		//25429,100,	//Mythril_Ore
		//25429,100,	//Mythril_Ore
		//25429,70,	//Mythril_Ore
		//25429,70,	//Mythril_Ore
		//25429,70,	//Mythril_Ore
		//25429,40,	//Mythril_Ore
		//25429,40,	//Mythril_Ore
		//25429,40,	//Mythril_Ore
		//25429,10,	//Mythril_Ore
		//25429,10,	//Mythril_Ore
		//25429,10,	//Mythril_Ore
		990,100,	//Boody_Red
		990,20,		//Boody_Red
		990,20,		//Boody_Red
        991,100,	//Crystal_Blue
		991,20,		//Crystal_Blue
		992,100,	//Wind_Of_Verdure 
		992,20,		//Wind_Of_Verdure 
		993,100,	//Yellow_Live
		993,20,		//Yellow_Live
		6635,10;	//Blacksmith Blessing
	}
	getmapxy(.@map$,.@x,.@y,BL_NPC,instance_npcname(strnpcinfo(0)));
	
	specialeffect 10; //55462
	specialeffect 307; //Forest Light
	hideonnpc instance_npcname(strnpcinfo(0)); //55462
	
	.@limit = getarraysize(.@reward);
	for(.@i = 1; .@i<=.@limit; .@i+=2) {
		if(rand(100) < (.@reward[.@i+1] + .@bonus))
			makeitem .@reward[.@i],1,.@map$,.@x+rand(3)-1,.@y+rand(3)-1;
	}
	// Shimmering_Crystal
	for(.@i = 0; .@i < 'unique_members + 5; ++.@i) {
		makeitem 25424, 1, .@map$, .@x+rand(3)-1, .@y+rand(3)-1;
	}
	// Azure Crystal
	for(.@i = 0; .@i < 'unique_members + 3; ++.@i) {
		makeitem 25475, 1, .@map$, .@x+rand(3)-1, .@y+rand(3)-1;
	}
	enablenpc instance_npcname("Manager Jurgens#prtc3");
	end;
	
OnInstanceInit:
	disablenpc instance_npcname(strnpcinfo(0)); //55462
	end;	
}

1@prtc,99,102,4	duplicate(Treasure Chest#prtc1)	Treasure Chest#prtc2	10005
1@prtc,99,105,4	duplicate(Treasure Chest#prtc1)	Treasure Chest#prtc3	10005
1@prtc,99,108,4	duplicate(Treasure Chest#prtc1)	Treasure Chest#prtc4	10005
1@prtc,99,111,4	duplicate(Treasure Chest#prtc1)	Treasure Chest#prtc5	10005
1@prtc,99,96,4	duplicate(Treasure Chest#prtc1)	Treasure Chest#prtc6	10005
1@prtc,99,93,4	duplicate(Treasure Chest#prtc1)	Treasure Chest#prtc7	10005
1@prtc,99,90,4	duplicate(Treasure Chest#prtc1)	Treasure Chest#prtc8	10005
1@prtc,99,87,4	duplicate(Treasure Chest#prtc1)	Treasure Chest#prtc9	10005
1@prtc,99,84,4	duplicate(Treasure Chest#prtc1)	Treasure Chest#prtc10	10005
1@prtc,99,81,4	duplicate(Treasure Chest#prtc1)	Treasure Chest#prtc11	10005
1@prtc,99,114,4	duplicate(Treasure Chest#prtc1)	Treasure Chest#prtc12	10005

1@prtc,104,28,4	script	Manager Jurgens#prtc3	853,{
	mes "["+strnpcinfo(1)+"]";
	mes "Thank you very much!!";
	next;
	mes "["+strnpcinfo(1)+"]";
	mes "Though, I'm kinda sure this place will be flooded again with these pests...";
	next;
	mes "["+strnpcinfo(1)+"]";
	mes "Wanna get out of here?";
	if(select("No:Yes")==1) close;
	// switch('Mode)	{
	// 	case 1: callfunc("DWM_Counter",4); break;
	// 	case 2: callfunc("DWM_Counter",5); break;
	// 	case 3: callfunc("DWM_Counter",6); break;
	// }
	warp "prt_fild05",284,220;
	end;

OnInstanceInit:
	disablenpc instance_npcname(strnpcinfo(0)); //55462
	end;	
}

