//===== rAthena Script =======================================
//= Ant Hell Instance (kRo Zero)
//============================================================

//Mapflag
1@ant	mapflag	nomemo
1@ant	mapflag	nosave	SavePoint
1@ant	mapflag	nowarp
1@ant	mapflag	nowarpto
1@ant	mapflag	noreturn
1@ant	mapflag	nobranch
1@ant	mapflag	noicewall
1@ant	mapflag	partylock
1@ant	mapflag	noteleport
1@ant	mapflag	monster_noteleport


// Instance Timer (custom)
1@ant,3,1,5	script	#ant_timer	CLEAR_NPC,{
	end;
OnStartTimer:
	initnpctimer;
	end;
OnStopTimer:
	//callfunc("F_InstanceTimer","ah",getnpctimer(0),'Mode);
	//callfunc("F_UpdateInsRecord");
	end;
OnTimer3600000:
	stopnpctimer;
	end;
OnInstanceInit:
	hideonnpc instance_npcname("#ant_timer");
	end;
}


//Main Script
1@ant,41,267,3	script	Memorial Society Member#ant2	702,9,9,{
	if(getcharid(0) != getpartyleader(getcharid(1),2)) {
		mes "["+strnpcinfo(1)+"]";
		mes "I wish to talk to your Party Leader.";
		close;
	}
	'party_id = getcharid(1);
	mes "["+strnpcinfo(1)+"]";
	mes "I need your help to find ^FF0000toxic steam geysers^000000 and seal them!";
	npctalk "I need your help to find toxic steam geysers and seal them!";
	next;
	mes "["+strnpcinfo(1)+"]";
	mes "Those geysers are endangering the lives of the natural habitat here..";
	npctalk "Those geysers are endangering the lives of the natural habitat here..";
	next;
	mes "["+strnpcinfo(1)+"]";
	mes "It's rather harmful to the ecosystem, don't you think so?";
	npctalk "It's rather harmful to the ecosystem, don't you think so?";
	next;
	mes "["+strnpcinfo(1)+"]";
	mes "Do you think that you can help us here?";
	npctalk "Do you think that you can help us here?";
	next;
	mes "["+strnpcinfo(1)+"]";
	mes "Good luck!";
	mes "I better get out of here before it gets worse..";
	npctalk "Good luck! I better get out of here before it gets worse..";
	close2;
		hideonnpc instance_npcname(strnpcinfo(0));
		donpcevent instance_npcname(strnpcinfo(0)) + "::OnStart";
		donpcevent instance_npcname("#ant_timer") + "::OnStartTimer";
	end;

OnInstanceInit:
	'Mode = 0;
	'Geyser_1 = 0;
	'Geyser_2 = 0;
	'Geyser_3 = 0;
	'Geyser_4 = 0;
	'Boss = 0;
	'Weaken = 0;
	'Map$ = instance_mapname("1@ant");
	end;

OnStart:
	//Retrieve Mode Modifiers
	.@arr$ = getvariableofnpc(.mode_config$,"#Instance_Config");
	explode(.@mod$,.@arr$,"-");
	for(.@i=0; .@i<getarraysize(.@mod$); .@i+=10) {
		if(atoi(.@mod$[.@i]) == 'Mode) {
			.@hp_mod = atoi(.@mod$[.@i+1]);
			.@atk_mod = atoi(.@mod$[.@i+2]);
			.@amt_mod = atoi(.@mod$[.@i+3]);
			'boss_hp_mod  = atoi(.@mod$[.@i+4]);
			'boss_atk_mod  = atoi(.@mod$[.@i+5]);
			'boss_def_mod  = atoi(.@mod$[.@i+6]);
			'boss_mdef_mod = atoi(.@mod$[.@i+7]);
			'boss_hit_mod  = atoi(.@mod$[.@i+8]);
			'boss_flee_mod = atoi(.@mod$[.@i+9]);
			break;
		}
	}

	//Spawn Dynamic Mobs
	setarray .@m_info,20077,25,20078,20,20079,25,20080,25;
	for(.@m=0; .@m<getarraysize(.@m_info); .@m+=2) {

		//Initialize Array
		deletearray $@mobid[0],getarraysize($@mobid);

		.@mob$ = .@m_info[.@m]+"_";
		.@amt = .@m_info[.@m+1] * .@amt_mod;
		areamonster 'Map$,42,42,247,254,"--ja--",.@m_info[.@m],.@amt;

		for(.@i = 0; .@i < getarraysize($@mobid); .@i++) {
			setd(".@"+.@mob$+.@i), $@mobid[.@i];
			if(unitexists(getd(".@"+.@mob$+.@i))) {
				getunitdata getd(".@"+.@mob$+.@i),.@data;
				setunitdata getd(".@"+.@mob$+.@i),UMOB_MAXHP,.@data[UMOB_MAXHP] * .@hp_mod;
				setunitdata getd(".@"+.@mob$+.@i),UMOB_HP,.@data[UMOB_HP] * .@hp_mod;
				setunitdata getd(".@"+.@mob$+.@i),UMOB_ATKMIN,.@data[UMOB_ATKMIN] * .@atk_mod;
				setunitdata getd(".@"+.@mob$+.@i),UMOB_ATKMAX,.@data[UMOB_ATKMAX] * .@atk_mod;
			}
		}
	}
	//Normal Mobs
	areamonster 'Map$,42,42,247,254,"--ja--",1005,10 * .@amt_mod;	//Familiar
	initnpctimer instance_npcname("#Toxic_Fumes");
	for(.@i = 1; .@i <= 4; .@i++) {
		hideoffnpc instance_npcname("Toxic Steam Geyser#ant"+.@i);
		enablenpc instance_npcname("Toxic Steam Geyser#ant"+.@i);
		initnpctimer instance_npcname("Toxic Steam Geyser#ant"+.@i);
	}
	end;

OnBoss:
	sleep 9000;
	instance_announce 0,"Maya: Trespasser, I'll bury you here with those who came before you!!",bc_map,"0xFFFFFF",FW_BOLD;

	//Initialize Array
	deletearray $@mobid[0],getarraysize($@mobid);

	if('Boss == 0) monster 'Map$,253,31,"--ja--",20076,1,instance_npcname("Memorial Society Member#ant2") + "::OnBossDead";
	'Boss = $@mobid[0];
	if(unitexists('Boss)) {
		unittalk 'Boss,"Trespasser, I will protect my kingdom from the likes of you!!";
		getunitdata 'Boss,.@boss_data;

		setunitdata 'Boss,UMOB_MAXHP,.@boss_data[UMOB_MAXHP] * 'boss_hp_mod;
		setunitdata 'Boss,UMOB_HP,.@boss_data[UMOB_HP] * 'boss_hp_mod;
	}

	//Save Stats
	'atk1_nm = .@boss_data[UMOB_ATKMIN] * 'boss_atk_mod;
	'atk2_nm = .@boss_data[UMOB_ATKMAX] * 'boss_atk_mod;
	'matk1_nm = .@boss_data[UMOB_MATKMIN] * 'boss_atk_mod;
	'matk2_nm = .@boss_data[UMOB_MATKMAX] * 'boss_atk_mod;
	'def_nm = .@boss_data[UMOB_DEF] * 'boss_def_mod;
	'mdef_nm = .@boss_data[UMOB_MDEF] * 'boss_mdef_mod;
	'hit_nm = .@boss_data[UMOB_HIT] * 'boss_hit_mod;
	'flee_nm = .@boss_data[UMOB_FLEE] * 'boss_flee_mod;

	//Set 'Weakened' Stats
	'atk1_wk = 'atk1_nm/2;
	'atk2_wk = 'atk2_nm/2;
	'matk1_wk = 'matk1_nm/2;
	'matk2_wk = 'matk2_nm/2;
	'def_wk = 'def_nm/2;
	'mdef_wk = 'mdef_nm/2;
	'hit_wk = 'hit_nm/2;
	'flee_wk = 'flee_nm/2;

	initnpctimer;
	donpcevent instance_npcname(strnpcinfo(0))+"::OnRestore";
	end;

OnTimer2500:
	if(unitexists('Boss)) {
		getunitdata 'Boss, .@arr;
		.@percent_hp = (.@arr[UMOB_HP] * 100) / .@arr[UMOB_MAXHP];
		.@label$ = instance_npcname("Memorial Society Member#ant2") + "::OnDarkCross";
		if(.@percent_hp / 10 >= 4) end;
	} else end;
OnTimer15000:
	if(unitexists('Boss)) {
		getunitdata 'Boss, .@arr;
		.@percent_hp = (.@arr[UMOB_HP] * 100) / .@arr[UMOB_MAXHP];
		.@label$ = instance_npcname("Memorial Society Member#ant2") + "::OnDarkCross";
		switch( .@percent_hp / 10 ) {
		case 10:
		case 9:
		case 8:
		case 7:
		case 6:
			break;
		case 5:
		case 4:
		case 3:
			monster 'Map$, .@arr[UMOB_X]-4, .@arr[UMOB_Y]+4, "#darkcross",3038,1, .@label$;
			monster 'Map$, .@arr[UMOB_X]+4, .@arr[UMOB_Y]+4, "#darkcross",3038,1, .@label$;
			monster 'Map$, .@arr[UMOB_X]-4, .@arr[UMOB_Y]-4, "#darkcross",3038,1, .@label$;
			monster 'Map$, .@arr[UMOB_X]+4, .@arr[UMOB_Y]-4, "#darkcross",3038,1, .@label$;
			sleep 3500;
			killmonster 'Map$, .@label$;
			break;
		case 2:
		case 1:
		case 0:
			monster 'Map$, .@arr[UMOB_X]-9, .@arr[UMOB_Y]+9, "#darkcross",3038,1, .@label$;
			monster 'Map$, .@arr[UMOB_X]+9, .@arr[UMOB_Y]+9, "#darkcross",3038,1, .@label$;
			monster 'Map$, .@arr[UMOB_X]+9, .@arr[UMOB_Y], "#darkcross",3038,1, .@label$;
			monster 'Map$, .@arr[UMOB_X]-9, .@arr[UMOB_Y], "#darkcross",3038,1, .@label$;
			monster 'Map$, .@arr[UMOB_X]-9, .@arr[UMOB_Y]-9, "#darkcross",3038,1, .@label$;
			monster 'Map$, .@arr[UMOB_X]+9, .@arr[UMOB_Y]-9, "#darkcross",3038,1, .@label$;
			monster 'Map$, .@arr[UMOB_X], .@arr[UMOB_Y]+9, "#darkcross",3038,1, .@label$;
			monster 'Map$, .@arr[UMOB_X], .@arr[UMOB_Y]-9, "#darkcross",3038,1, .@label$;
			sleep 5000;
			killmonster 'Map$, .@label$;
			break;
			break;
		}
	}
	end;

OnTimer20000:
	if(rand(1,4)==3) {
		'Weaken = 1;
		donpcevent instance_npcname(strnpcinfo(0))+"::OnWeaken";
		instance_announce 0,"Memorial Society Member: The toxic fumes has intensified. Maya seems to have been severely weakened.",bc_map,"0xFFFFFF",FW_BOLD;
		instance_announce 0,"Memorial Society Member: Take this opportunity to damage Maya!!.",bc_map,"0xFFFFFF",FW_BOLD;
		if(unitexists('Boss))
			unittalk 'Boss,"What sorcery is this? Why do I feel bad about this...?";
	}
	end;
OnTimer35000:
	if('Weaken == 1) {
		'Weaken = 0;
		donpcevent instance_npcname(strnpcinfo(0))+"::OnRestore";
		instance_announce 0,"The toxic fumes has dissipated. Maya has recovered from the toxic fumes.",bc_map,"0xFFFFFF",FW_BOLD;
		if(unitexists('Boss))
			unittalk 'Boss,"I won't let myself be brought down by this!";
	}
	initnpctimer;
	end;

OnWeaken:
	if(unitexists('Boss)) {
		setunitdata 'Boss,UMOB_ATKMIN,'atk1_wk;
		setunitdata 'Boss,UMOB_ATKMAX,'atk2_wk;
		setunitdata 'Boss,UMOB_MATKMIN,'matk1_wk;
		setunitdata 'Boss,UMOB_MATKMAX,'matk1_wk;
		setunitdata 'Boss,UMOB_DEF,'def_wk;
		setunitdata 'Boss,UMOB_MDEF,'mdef_wk;
		setunitdata 'Boss,UMOB_HIT,'hit_wk;
		setunitdata 'Boss,UMOB_FLEE,'flee_wk;
	}
	end;
OnRestore:
	if(unitexists('Boss)) {
		setunitdata 'Boss,UMOB_ATKMIN,'atk1_nm;
		setunitdata 'Boss,UMOB_ATKMAX,'atk2_nm;
		setunitdata 'Boss,UMOB_MATKMIN,'matk1_nm;
		setunitdata 'Boss,UMOB_MATKMAX,'matk1_nm;
		setunitdata 'Boss,UMOB_DEF,'def_nm;
		setunitdata 'Boss,UMOB_MDEF,'mdef_nm;
		setunitdata 'Boss,UMOB_HIT,'hit_nm;
		setunitdata 'Boss,UMOB_FLEE,'flee_nm;
	}
	end;

OnDarkCross:
	end;
OnBossDead:
	stopnpctimer;
	if ('Mode == 1) {
		enablenpc instance_npcname("Treasure Chest#ant1");
	} else {
		'unique_members = min(MAX_PARTY, callfunc("F_GetUniquePartyMemberCountInMap", getcharid(1), 'Map$)); // Cap to MAX_PARTY
		'RewardUniqueIDLogs$ = "";
		for (.@i = 1; .@i <= 'unique_members; ++.@i) {
			enablenpc instance_npcname("Treasure Chest#ant" + .@i);
		}
	}
	donpcevent instance_npcname("#ant_timer") + "::OnStopTimer";
	end;
}

1@ant,1,1,3	script	#Toxic_Fumes	-1,{
end;

OnTimer100:
	if('Geyser_1 == 1 && 'Geyser_3 == 1) {
		.@label$ = instance_npcname(strnpcinfo(0))+"::OnFumes";
		killmonster 'Map$, .@label$;
		'Fumes = 0;
		stopnpctimer;
		end;
	}
OnTimer2000:
	.@label$ = instance_npcname(strnpcinfo(0))+"::OnFumes";
	areamonster 'Map$,127,24,218,300,"#toxicfumes",2536,150,.@label$;	//Toxic Fumes 2536
	end;
OnTimer4000:
	.@label$ = instance_npcname(strnpcinfo(0))+"::OnFumes";
	killmonster 'Map$, .@label$;
	initnpctimer;
	end;
OnFumes:
	end;
}

1@ant,38,49,4	script	Toxic Steam Geyser#ant1	4_ENERGY_BLACK,{
	if(!'Geyser_1) {
		specialeffect2 12;
		progressbar "ffff00",10;
		'Geyser_1 = 1;
		message strcharinfo(0),"You have managed to seal this toxic geyser!";
		.@sum = ('Geyser_1 + 'Geyser_2 + 'Geyser_3 + 'Geyser_4);
		if(.@sum == 4) donpcevent instance_npcname("Memorial Society Member#ant2") + "::OnBoss";
			else instance_announce 0,"Remaining Toxic Steam Geysers to seal: "+(4-.@sum),bc_map,"0xFFFFFF",FW_BOLD;
		hideonnpc instance_npcname(strnpcinfo(0));
	} else {
		message strcharinfo(0),"I should head to the other geysers...";
	}
	end;

OnTimer9000:
	specialeffect 247;
	initnpctimer;
	end;
OnInstanceInit:
	hideonnpc instance_npcname(strnpcinfo(0));
	disablenpc instance_npcname(strnpcinfo(0));
	end;
}

1@ant,247,261,4	script	Toxic Steam Geyser#ant2	4_ENERGY_BLACK,{
	if(!'Geyser_2) {
		specialeffect2 12;
		progressbar "ffff00",10;
		'Geyser_2 = 1;
		message strcharinfo(0),"You have managed to seal this toxic geyser!";
		.@sum = ('Geyser_1 + 'Geyser_2 + 'Geyser_3 + 'Geyser_4);
		if(.@sum == 4) donpcevent instance_npcname("Memorial Society Member#ant2") + "::OnBoss";
			else instance_announce 0,"Remaining Toxic Steam Geysers to seal: "+(4-.@sum),bc_map,"0xFFFFFF",FW_BOLD;
		hideonnpc instance_npcname(strnpcinfo(0));
	} else {
		message strcharinfo(0),"I should head to the other geysers...";
	}
	end;

OnTimer9000:
	specialeffect 247;
	initnpctimer;
	end;
OnInstanceInit:
	hideonnpc instance_npcname(strnpcinfo(0));
	disablenpc instance_npcname(strnpcinfo(0));
	end;
}

1@ant,81,258,4	script	Toxic Steam Geyser#ant3	4_ENERGY_BLACK,{
	if(!'Geyser_3) {
		specialeffect2 12;
		progressbar "ffff00",10;
		'Geyser_3 = 1;
		message strcharinfo(0),"You have managed to seal this toxic geyser!";
		.@sum = ('Geyser_1 + 'Geyser_2 + 'Geyser_3 + 'Geyser_4);
		if(.@sum == 4) donpcevent instance_npcname("Memorial Society Member#ant2") + "::OnBoss";
			else instance_announce 0,"Remaining Toxic Steam Geysers to seal: "+(4-.@sum),bc_map,"0xFFFFFF",FW_BOLD;
		hideonnpc instance_npcname(strnpcinfo(0));
	} else {
		message strcharinfo(0),"I should head to the other geysers...";
	}
	end;

OnTimer9000:
	specialeffect 247;
	initnpctimer;
	end;
OnInstanceInit:
	hideonnpc instance_npcname(strnpcinfo(0));
	disablenpc instance_npcname(strnpcinfo(0));
	end;
}

1@ant,239,32,4	script	Toxic Steam Geyser#ant4	4_ENERGY_BLACK,{
	if(!'Geyser_4) {
		specialeffect2 12;
		progressbar "ffff00",10;
		'Geyser_4 = 1;
		message strcharinfo(0),"You have managed to seal this toxic geyser!";
		.@sum = ('Geyser_1 + 'Geyser_2 + 'Geyser_3 + 'Geyser_4);
		if(.@sum == 4) donpcevent instance_npcname("Memorial Society Member#ant2") + "::OnBoss";
			else instance_announce 0,"Remaining Toxic Steam Geysers to seal: "+(4-.@sum),bc_map,"0xFFFFFF",FW_BOLD;
		hideonnpc instance_npcname(strnpcinfo(0));
	} else {
		message strcharinfo(0),"I should head to the other geysers...";
	}
	end;

OnTimer9000:
	specialeffect 247;
	initnpctimer;
	end;
OnInstanceInit:
	hideonnpc instance_npcname(strnpcinfo(0));
	disablenpc instance_npcname(strnpcinfo(0));
	end;
}


1@ant,255,61,4	script	Treasure Chest#ant1	4_TREASURE_BOX,{
	.@unique_id = callfunc("F_GetUniqueID", getcharid(3));
	if ('Mode >= 2) {
		if (strpos('RewardUniqueIDLogs$, ""+.@unique_id) != -1) {
			end;
		}
		'RewardUniqueIDLogs$ = 'RewardUniqueIDLogs$ + "||" + .@unique_id;
	}

	set .@ins_map$,strcharinfo(3);
	getpartymember(getcharid(1)),2;
	for( .@i = 0; .@i < $@partymembercount; .@i++ ) {
		if (attachrid($@partymemberaid[.@i])) {
			if (strcharinfo(3) == .@ins_map$) {
				.@party_inmap++;
			}
			detachrid;
		}
	}
	if(.@party_inmap >=6 && .@party_inmap <12) .@bonus = 5;
		else if (.@party_inmap >=12) .@bonus = 15;
		else .@bonus = 0;

	if('Mode <= 2) {
		setarray .@reward[1],
		25424,100,	//Shimmering_Crystal
		25424,70,	//Shimmering_Crystal
		25424,40,	//Shimmering_Crystal
		29587,50,	//Shimmering_Crystal_Box
		29587,25,	//Shimmering_Crystal_Box
		29586,5,	//Refine_Box
		29586,10,	//Refine_Box
		29584,1,	//Rune_Box
		29584,1,	//Rune_Box
		29585,5,	//Pvm_Box
		29585,3,	//Pvm_Box
		18005,50,	//Mixed_Rune_Fragment
		18005,1,	//Mixed_Rune_Fragment
		23649,100,	//Gel_Fragment_Box
		23649,70,	//Gel_Fragment_Box
		23649,40,	//Gel_Fragment_Box
		29588,50,	//Azure_Crystal_Box
		29588,25,	//Azure_Crystal_Box
		29588,10,	//Azure_Crystal_Box
		25475,100,	//Azure_Crystal
		25475,70,	//Azure_Crystal
		25475,40,	//Azure_Crystal
		6635,1;		//Blacksmith Blessing
	} else if ('Mode == 3) {
		setarray .@reward[1],
		//25460,100,	//Bloody_Ruby
		//25460,50,	//Bloody_Ruby
		//25460,10,	//Bloody_Ruby
		29586,20,	//Refine_Box
		29586,10,	//Refine_Box
		29584,2,	//Rune_Box
		29585,5,	//Pvm_Box
		29584,2,	//Rune_Box
		29585,5,	//Pvm_Box
		18005,100,	//Mixed_Rune_Fragment
		29588,100,	//Azure_Crystal_Box
		29588,50,	//Azure_Crystal_Box
		29587,100,	//Shimmering_Crystal_Box
		29587,50,	//Shimmering_Crystal_Box
		756,100,	//Oridecon_Stone
		756,100,	//Oridecon_Stone
		756,20,		//Oridecon_Stone
		756,20,		//Oridecon_Stone
		756,20,		//Oridecon_Stone
		756,20,		//Oridecon_Stone
		757,100,	//Elunium_Stone
		757,100,	//Elunium_Stone
		757,20,		//Elunium_Stone
		757,20,		//Elunium_Stone
		757,20,		//Elunium_Stone
		757,20,		//Elunium_Stone
		984,100,	//Oridecon
		984,15,		//Oridecon
		984,15,		//Oridecon
		985,100,	//Elunium
		985,15,		//Elunium
		985,15,		//Elunium
		//25429,100,	//Mythril_Ore
		//25429,100,	//Mythril_Ore
		//25429,100,	//Mythril_Ore
		//25429,70,	//Mythril_Ore
		//25429,70,	//Mythril_Ore
		//25429,70,	//Mythril_Ore
		//25429,40,	//Mythril_Ore
		//25429,40,	//Mythril_Ore
		//25429,40,	//Mythril_Ore
		//25429,10,	//Mythril_Ore
		//25429,10,	//Mythril_Ore
		//25429,10,	//Mythril_Ore
		990,100,	//Boody_Red
		990,20,		//Boody_Red
        991,100,	//Crystal_Blue
		991,20,		//Crystal_Blue
		992,100,	//Wind_Of_Verdure
		992,20,		//Wind_Of_Verdure
		993,100,	//Yellow_Live
		993,20,		//Yellow_Live
		6635,10;	//Blacksmith Blessing
	}
	getmapxy(.@map$,.@x,.@y,BL_NPC,instance_npcname(strnpcinfo(0)));

	specialeffect 10; //55462
	specialeffect 307; //Forest Light
	hideonnpc instance_npcname(strnpcinfo(0)); //55462

	.@limit = getarraysize(.@reward);
	for(.@i = 1; .@i<=.@limit; .@i+=2) {
		if(rand(100) < (.@reward[.@i+1] + .@bonus))
			makeitem .@reward[.@i],1,.@map$,.@x+rand(3)-1,.@y+rand(3)-1;
	}
	// Azure Crystal
	for(.@i = 0; .@i < 'unique_members + 5; ++.@i) {
		makeitem 25475, 1, .@map$, .@x+rand(3)-1, .@y+rand(3)-1;
	}
	// Crimson Magic Crystal
	for(.@i = 0; .@i < 'unique_members + 3; ++.@i) {
		makeitem 25476, 1, .@map$, .@x+rand(3)-1, .@y+rand(3)-1;
	}
	enablenpc instance_npcname("Memorial Society Member#ant3");
	end;

OnInstanceInit:
	disablenpc instance_npcname(strnpcinfo(0)); //55462
	end;
}

1@ant,251,64,4	duplicate(Treasure Chest#ant1)	Treasure Chest#ant2	10005
1@ant,247,61,4	duplicate(Treasure Chest#ant1)	Treasure Chest#ant3	10005
1@ant,249,57,4	duplicate(Treasure Chest#ant1)	Treasure Chest#ant4	10005
1@ant,252,54,4	duplicate(Treasure Chest#ant1)	Treasure Chest#ant5	10005
1@ant,256,51,4	duplicate(Treasure Chest#ant1)	Treasure Chest#ant6	10005
1@ant,259,53,4	duplicate(Treasure Chest#ant1)	Treasure Chest#ant7	10005
1@ant,262,57,4	duplicate(Treasure Chest#ant1)	Treasure Chest#ant8	10005
1@ant,264,61,4	duplicate(Treasure Chest#ant1)	Treasure Chest#ant9	10005
1@ant,260,62,4	duplicate(Treasure Chest#ant1)	Treasure Chest#ant10	10005
1@ant,255,68,4	duplicate(Treasure Chest#ant1)	Treasure Chest#ant11	10005
1@ant,250,68,4	duplicate(Treasure Chest#ant1)	Treasure Chest#ant12	10005

1@ant,259,64,4	script	Memorial Society Member#ant3	702,{
	mes "["+strnpcinfo(1)+"]";
	mes "You have completed your objective!";
	mes "I think that we will meet again.";
	next;
	mes "["+strnpcinfo(1)+"]";
	mes "Wanna get out of here?";
	if(select("No:Yes")==1) close;
	// switch('Mode)	{
	// 	case 1: callfunc("DWM_Counter",10); break;
	// 	case 2: callfunc("DWM_Counter",11); break;
	// 	case 3: callfunc("DWM_Counter",12); break;
	// }
	warp "prt_fild06",47,198;
	end;

OnInstanceInit:
	disablenpc instance_npcname(strnpcinfo(0)); //55462
	end;
}
