//===== rAthena Script =======================================
//= Instance script configuration (kRO Zero)
//============================================================

-	script	#Instance_Config	-1,{

OnInit:
	//ID, Instance, Min Level, Max Level, CD Quest, ...
	setarray .level_arr$,
		1,"Poring Village",30,99,30101,50,99,12417,80,99,30102,
		2,"Prontera Culvert",50,99,30103,50,99,12425,50,99,30104,
		3,"Orc's Memory",50,99,30105,50,99,12420,50,99,30106,
		4,"Ant Hell",70,99,30107,70,99,12438,70,99,30108,
		5,"Izlude Deepsea",80,99,30109,80,99,12439,80,99,30110,
		6,"Sunken Ship",90,99,30111,90,99,12440,90,99,30112,
		7,"Past Odin Temple",90,99,30113,90,99,8563,99,99,30114,
		8,"Bloody Mansion",60,99,30115,60,99,30116,99,99,30117,30,99,30118
		;
	.level_config$ = implode(.level_arr$,"-");
		
	//Mode, MobHP, MobAtk, MobAmt, BossHP, BossAtk, BossDef, BossMdef, BossHit, BossFlee
	setarray .mode_arr$,
		1,1,1,1,1,1,1,1,1,1,	//Easy
		2,3,2,2,2,2,1,1,2,1, 	//Normal
		3,6,4,5,5,5,2,2,3,1	//Hard
		;
	.mode_config$ = implode(.mode_arr$,"-");
	
	//Menu
	setarray .menu_arr$, 
		"^008000Easy Challenges^000000 suit me",
		"I like ^0000FFNormal Challenges^000000",
		"Nothing's ^FF0000 Hard^000000 for me, bring it on!";
	.menu_config$ = implode(.menu_arr$,":");
	end;

}

//Common Functions
function	script	F_ChkInsCD	{
	.@quest = getarg(0);

	if(isbegin_quest(.@quest) == 0) return;
	.@cd = checkquest(.@quest,PLAYTIME);
	if(.@cd == 2) {
		erasequest .@quest;
		mes "^0000ff- You may access this memorial dungeon -^000000";
		close;
	}
	if(.@cd == 0 || .@cd == 1) {
		mes "^ff0000- You have to wait 24h, the cooldown isn't gone yet ! -^000000";
		close;
	}

	return;
}

function	script	F_ChkInsLv	{
	.@min_level = getarg(0);
	.@max_level = getarg(1);
	
	//If instance limit is 99 and player level is 99
	if(BaseLevel >= .@max_level && .@max_level == 99) return;
	
	//If instance limit is below 99
	if(BaseLevel < .@min_level || BaseLevel > .@max_level) {
		if(.@min_level != .@max_level) mes "- This instance is only for Level ^FF0000"+.@min_level+ " - "+.@max_level+"^000000 Players.";
			else mes "- This instance is only for Level ^FF0000"+.@min_level+"^000000 Players.";
		close;
	}

	return;
}

