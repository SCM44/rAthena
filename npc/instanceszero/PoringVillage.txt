//===== rAthena Script =======================================
//= Poring Village (kRo Zero)
//============================================================

//Mapflag
1@begi	mapflag	nomemo
1@begi	mapflag	nosave	SavePoint
1@begi	mapflag	nowarp
1@begi	mapflag	nowarpto
1@begi	mapflag	noreturn
1@begi	mapflag	nobranch
1@begi	mapflag	noicewall
1@begi	mapflag	partylock
1@begi	mapflag	noteleport
1@begi	mapflag	monster_noteleport

// Instance Timer (custom)
1@begi,3,1,5	script	#begi_timer	CLEAR_NPC,{
	end;
OnStartTimer:
	initnpctimer;
	end;
OnStopTimer:
	//callfunc("F_InstanceTimer","pv",getnpctimer(0),'Mode);
	//callfunc("F_UpdateInsRecord");
	end;
OnTimer3600000:
	stopnpctimer;
	end;
OnInstanceInit:
	hideonnpc instance_npcname("#begi_timer");
	end;
}

//Entrance NPC
prt_fild05,141,236,3	script	Curious Adventurer#begi	671,{

	//Give all 1st timers
	if(Ins_PV_FirstRun == 0) {
		mes "["+strnpcinfo(1)+"]";
		mes "Here's something I think you deserve.";
		mes "A little something to commemorate defeating the porings!";
		Ins_PV_FirstRun = 1;
		getitem F_Rand(19238,19239),1;			
		close;
	}
	
	.@party_id = getcharid(1);
	.@ins_mas = getpartyleader(.@party_id,2);
	.@p_name$ = getpartyname(.@party_id);
	.@md_name$ = "Poring Village";

	mes "["+strnpcinfo(1)+"]";
	mes "Hello!";
	mes "Are you new around here?";
	next;
	mes "["+strnpcinfo(1)+"]";
	mes "I'm quite excited to see you!";
	mes "There's a ^0000FFlittle village^000000 ahead, would you like to come with me?";
	next;
	mes "["+strnpcinfo(1)+"]";
	mes "I'm scared to go alone.";
	mes "Together, we can explore what's in there.";
	next;
	mes "["+strnpcinfo(1)+"]";
	mes "Are you ready?";
	mes "...We will face whatever ahead with...^0000FFCOURAGE^000000";
	next;
	if(getcharid(0) == .@ins_mas) .@str$ = "Show me the way to ^FF0000Poring Village^000000!";
	
	switch(select(.@str$,"Enter ^FF0000Poring Village^000000","Cancel")) {
	case 1:
		next;
		
		//Load Config Settings
		.@select = select(getvariableofnpc(.menu_config$,"#Instance_Config"));
		.@arr$ = getvariableofnpc(.level_config$,"#Instance_Config");
		explode(.@config$,.@arr$,"-");
		for(.@i = 0; .@i<getarraysize(.@config$); .@i+=11) {
			if(.@config$[.@i+1] == .@md_name$) {
				.@quest = atoi(.@config$[.@i+4 + 3*(.@select-1)]);
				.@min_level = atoi(.@config$[.@i+2 + 3*(.@select-1)]);
				.@max_level = atoi(.@config$[.@i+3 + 3*(.@select-1)]);
				break;
			}
		}
		//Check Cooldown
		callfunc("F_ChkInsCD",.@quest);	
		//Check Level
		callfunc("F_ChkInsLv",.@min_level,.@max_level);
		
		//Create Instance
		.@inst_id = instance_create(.@md_name$);
		if (.@inst_id < 0) {
			mes "Party Name: "+ getpartyname(.@party_id);
			mes "Party Leader: "+strcharinfo(0);
			mes "^0000ff"+.@md_name$+" ^000000 - Reservation Failed.";
			close;
		}

		setinstancevar('Mode,.@select,.@inst_id);
		
		//debugmes "Instance Creation";

		mes "["+strnpcinfo(1)+"]";
		mes "Would you like to enter now?";
		if(select("Yes, I'd like to:Later")==2) close;
	case 2:
		if(instance_id() != 0) {
			.@mode = getinstancevar('Mode,instance_id(IM_PARTY));
			//Load Config Settings
			.@arr$ = getvariableofnpc(.level_config$,"#Instance_Config");
			explode(.@config$,.@arr$,"-");
			for(.@i = 0; .@i<getarraysize(.@config$); .@i+=11) {
				if(.@config$[.@i+1] == .@md_name$) {
					.@quest = atoi(.@config$[.@i+4 + 3*(.@mode-1)]);
					.@min_level = atoi(.@config$[.@i+2 + 3*(.@mode-1)]);
					.@max_level = atoi(.@config$[.@i+3 + 3*(.@mode-1)]);
					break;
				}
			}
			//Check Cooldown
			callfunc("F_ChkInsCD",.@quest);	
			//Check Level
			callfunc("F_ChkInsLv",.@min_level,.@max_level);
		}
		
		mes "["+strnpcinfo(1)+"]";
		switch( instance_enter(.@md_name$) ) {
		case IE_OTHER:
			mes "An unknown error has occurred.";
			close;
		case IE_NOINSTANCE:
			mes "The memorial dungeon "+.@md_name$+" does not exist.";
			mes "The party leader did not generate the dungeon yet.";
			close;
		case IE_NOMEMBER:
			mes "Only the registered members can enter the instance "+.@md_name$+".";
			close;
		case IE_OK:
			setquest .@quest;
			break;
		}
		//debugmes "Instance Entrance Poring Village "+strcharinfo(0)+".";
		close;
	case 3: close;
	}
	
OnInit:
	waitingroom "Poring Village",0;
	end;
}

1@begi,139,39,3	script	Curious Adventurer#begi2	671,{
	'party_id = getcharid(1);
	
	//Check if Party Leader
	callfunc "F_ChkInstancePT","Curious Adventurer#begi2";
	
	mes "["+strnpcinfo(1)+"]";
	mes "Are you ready? I shall help clear the way~";
	npctalk "Are you ready? I shall help clear the way~";
	next;
	mes "["+strnpcinfo(1)+"]";
	mes "You see those porings?";
	npctalk "You see those porings?";
	next;
	mes "["+strnpcinfo(1)+"]";
	mes "Kill every one of them.";
	npctalk "Kill every one of them.";
	next;
	mes "["+strnpcinfo(1)+"]";
	mes "It's straight foward. Once you kill a set amount, a boss will appear!";
	npctalk "It's straight foward. Once you kill a set amount, a boss will appear!";
	next;
	mes "["+strnpcinfo(1)+"]";
	mes "So ready or not....Here we go!!";
	npctalk "So ready or not....Here we go!!";
	
	hideonnpc instance_npcname(strnpcinfo(0));
	instance_announce instance_id(), "[Curious Adventurer] Good luck my friend!", bc_map, 0x00FF00;
	specialeffect 90;
	disablenpc instance_npcname( "alapovibar", instance_id() );
	disablenpc instance_npcname( "alapovibar2", instance_id() );
	disablenpc instance_npcname( "alapovibar3", instance_id() );
	disablenpc instance_npcname( "alapovibar4", instance_id() );
	disablenpc instance_npcname( "alapovibar5", instance_id() );
	disablenpc instance_npcname( "alapovibar6", instance_id() );
	setcell instance_mapname( "1@begi", instance_id() ),145,35,145,41,cell_walkable,1;
	donpcevent instance_npcname("#begi_timer") + "::OnStartTimer"; 
	donpcevent instance_npcname("poring_village") + "::OnSummon"; 
	end;

}

1@begi,101,107,4	script	poring_village	HIDDEN_WARP_NPC,2,2,{
	function summon_normal;
	function summon_guardian;
	end;

OnSummon: //Define monster list based on mode
	//Poring, Drops, Poporing, Marin, Goldring, Amering, King Poring
	switch('Mode) {
		case 1: setarray 'List,3815,3813,3814,3816,3811,3812,3810; break;
		case 2: setarray 'List,3815,3813,3814,3816,3811,3812,3810; break; 
		case 3: setarray 'List,1810,1808,1809,1811,1806,1807,1805; break;
	}
	summon_normal;
	end;
	
OnInstanceInit:
	'Mode = 0;
	'Map$ = instance_mapname(strnpcinfo(4));
	'NPC_Name$ = instance_npcname( strnpcinfo(0) );
	
	'Boss_Stage = 0;
	.timerCount = 0;

	//initnpctimer;
	end;

function	summon_normal	{
	'Boss_Stage++;
	
	switch( 'Boss_Stage ){
		case 1:
			set .@clabel$, 'NPC_Name$ + "::OnNormalDied";
			//poring, drops, poporing
			areamonster 'Map$,149,39,185,35,"--ja--",'List[0],10,.@clabel$;
			areamonster 'Map$,149,39,185,35,"--ja--",'List[3],10,.@clabel$;
			areamonster 'Map$,187,57,87,68,"--ja--",'List[1],15,.@clabel$;
			areamonster 'Map$,187,57,87,68,"--ja--",'List[2],10,.@clabel$;
			areamonster 'Map$,75,50,19,93,"--ja--",'List[3],15,.@clabel$;
			areamonster 'Map$,75,50,19,93,"--ja--",'List[2],15,.@clabel$;
			areamonster 'Map$,75,50,19,93,"--ja--",'List[0],10,.@clabel$;
			break;
		case 2:
			set .@clabel$, 'NPC_Name$ + "::OnNormalDied";
			//drops, marin, poporing
			areamonster 'Map$,148,106,219,109,"--ja--",'List[3],10,.@clabel$;
			areamonster 'Map$,148,106,219,109,"--ja--",'List[1],10,.@clabel$;
			areamonster 'Map$,219,109,194,146,"--ja--",'List[2],15,.@clabel$;
			areamonster 'Map$,219,109,194,146,"--ja--",'List[3],10,.@clabel$;
			areamonster 'Map$,172,155,62,141,"--ja--",'List[1],15,.@clabel$;
			areamonster 'Map$,172,155,62,141,"--ja--",'List[2],15,.@clabel$;
			areamonster 'Map$,172,155,62,141,"--ja--",'List[2],10,.@clabel$;
			break;
		case 3:
			set .@clabel$, 'NPC_Name$ + "::OnNormalDied";
			//poring, drops, marin, poporing
			areamonster 'Map$,46,185,113,203,"--ja--",'List[0],5,.@clabel$;
			areamonster 'Map$,46,185,113,203,"--ja--",'List[1],7,.@clabel$;
			areamonster 'Map$,46,185,113,203,"--ja--",'List[2],8,.@clabel$;
			areamonster 'Map$,46,185,113,203,"--ja--",'List[3],10,.@clabel$;
			break;
	}
	return;
}

function	summon_guardian	{
	.@label$ = 'NPC_Name$ + "::OnGuardianDied";
			
	switch( 'Boss_Stage ){
		case 1:
			//Gold Poring
			setarray .@monster,'List[4];
			setarray .@coordinate,100, 117, 135, 94;
			break;
		case 2:
			//Amering
			setarray .@monster,'List[5];
			setarray .@coordinate,30, 171, 60, 141;
			break;
		case 3:
			//Almighty King of Porings
			setarray .@monster,'List[6];
			setarray .@coordinate,157,207,198,183;
			break;
	}
	.@monster_size = getarraysize( .@monster );
	for( .@i=0; .@i < .@monster_size; .@i++ ) {
		areamonster 'Map$,.@coordinate[0],.@coordinate[1],.@coordinate[2],.@coordinate[3],"--ja--",.@monster[.@i],1,.@label$,2;
	}
	return;
}
	
OnNormalDied:
	.@remaining = mobcount('Map$,'NPC_Name$+"::OnNormalDied" );
	
	if((.@remaining > 10 && .@remaining%5 == 0) || .@remaining <= 10)
		instance_announce 0,"Remaining Poring Villagers : "+.@remaining,bc_map;
	
	if( .@remaining == 0 ){
		sleep2 500;
		instance_announce 0,"All of the Stage "+'Boss_Stage+" Poring Villagers have been defeated!!",bc_map;
		if('Boss_Stage == 3) {
			instance_announce instance_id(), "[Almighty King of Porings] What is this? You damn trespasser!", bc_map, 0xFF0000;
			sleep2 1500;
			instance_announce instance_id(), "[Almighty King of Porings] Can't you humans leave alone in peace??", bc_map, 0xFF0000;
			sleep2 1500;
			instance_announce instance_id(), "[Almighty King of Porings] You will regret being here.. I shall exterminate you!!", bc_map, 0xFF0000;
			sleep2 1500;
		}
		summon_guardian;
	}
	end;
	
OnGuardianDied:
	.@remaining = mobcount('Map$,'NPC_Name$+"::OnGuardianDied" );
	
	if( !.@remaining ){
		sleep 500;
		
		//Boss Stage
		switch('Boss_Stage) {
		case 1:
			instance_announce instance_id(), "[Goldring] Don't be fooled human, this is just a beginning!!", bc_map, 0xFF0000;
			break;
		case 2:
			instance_announce instance_id(), "[Amering] We cannot be stopped, go back while you still can!!", bc_map, 0xFF0000;
			break;			
		case 3:
			donpcevent instance_npcname("#begi_timer") + "::OnStopTimer";
			enablenpc instance_npcname("Treasure Chest#begi");
		
			instance_announce instance_id(), "[Almighty King of Porings] Though I have been defeated..", bc_map, 0xFF0000;
			sleep 1500;
			instance_announce instance_id(), "[Almighty King of Porings] Don't be conceited. I will be avenged.", bc_map, 0xFF0000;
			sleep 1500;
			instance_announce instance_id(), "[Almighty King of Porings] I shall send an army of Porings to hunt you down!!", bc_map, 0xFF0000;
			enablenpc instance_npcname( "alapovivig", instance_id() );
			end;
		}
		
		sleep 500;
		summon_normal;
	}
	end;
	
}

//Barricades
1@begi,146,35,5	script	Barricade::alapovibar	4_ROPEPILE,2,2,{
	end;

OnInstanceInit:
OnEnable:
	setcell instance_mapname( "1@begi", instance_id() ),145,35,145,41,cell_walkable,0;
	enablenpc;
	end;
}

1@begi,146,36,5	duplicate(alapovibar)	Barricade::alapovibar2	4_ROPEPILE
1@begi,146,37,5	duplicate(alapovibar)	Barricade::alapovibar3	4_ROPEPILE
1@begi,146,38,5	duplicate(alapovibar)	Barricade::alapovibar4	4_ROPEPILE
1@begi,146,39,5	duplicate(alapovibar)	Barricade::alapovibar5	4_ROPEPILE
1@begi,146,40,5	duplicate(alapovibar)	Barricade::alapovibar6	4_ROPEPILE

//Stat Buff
1@begi,117,109,5	script	Strange Light#begi1	10043,3,3,{
end;

OnTouch_:
	specialeffect 247;
	transform 1680,250000; //Hillwind
	sc_start SC_STRFOOD,250000,15;
	sc_start SC_AGIFOOD,250000,15;
	sc_start SC_VITFOOD,250000,15;
	sc_start SC_DEXFOOD,250000,15;
	sc_start SC_INTFOOD,250000,15;
	sc_start SC_LUKFOOD,250000,15;
	hideonnpc instance_npcname(strnpcinfo(0));
	sleep 30000;
	hideoffnpc instance_npcname(strnpcinfo(0));
	end;
}

1@begi,40,167,5	duplicate(Strange Light#begi1)	Strange Light#begi2	10043,3,3
1@begi,175,200,5	duplicate(Strange Light#begi1)	Strange Light#begi3	10043,3,3


1@begi,173,193,4	script	Treasure Chest#begi	10005,{
	//Check if Party Leader
	callfunc "F_ChkInstancePT","";
	//Log Event
	//debugmes "Instance Completion Poring Village";
	
	.@ins_map$ = strcharinfo(3);
	getpartymember(getcharid(1)),2;
	for( .@i = 0; .@i < $@partymembercount; .@i++ ) {
		if (attachrid($@partymemberaid[.@i])) {
			if (strcharinfo(3) == .@ins_map$) {
				.@party_inmap++;
			}
			detachrid;
		}
	}
	if(.@party_inmap >=7 && .@party_inmap <12) .@bonus = 7;
		else if (.@party_inmap >=12) .@bonus = 15;
		else .@bonus = 0;
	
	//Jello Fragment Box, Jello Fragments
	if(gettime(DT_DAYOFWEEK) == MONDAY) .@frag = 25465;
	if(gettime(DT_DAYOFWEEK) == TUESDAY) .@frag = 25466;
	if(gettime(DT_DAYOFWEEK) == WEDNESDAY) .@frag = 25467;
	if(gettime(DT_DAYOFWEEK) == THURSDAY) .@frag = 25468;
	if(gettime(DT_DAYOFWEEK) == FRIDAY) .@frag = 25469;
	
	if(
		(gettime(DT_DAYOFWEEK) == SATURDAY || gettime(DT_DAYOFWEEK) == SUNDAY) ||
		'Mode == 3
	) .@frag = 23649;
	
	
		setarray .@reward[1],
		
		11565,100,	//White_Potion_B
		11565,100,	//White_Potion_B
		11565,100,	//White_Potion_B
		11565,100,	//White_Potion_B
		11565,100,	//White_Potion_B
		11565,100,	//White_Potion_B
		11565,10,	//White_Potion_B
		11565,10,	//White_Potion_B
		11565,10,	//White_Potion_B
		11565,10,	//White_Potion_B
		11566,100,	//Yellow_Potion_
		11566,100,	//Yellow_Potion_
		11566,100,	//Yellow_Potion_
		11566,100,	//Yellow_Potion_
		11566,100,	//Yellow_Potion_
		11566,100,	//Yellow_Potion_
		11566,10,	//Yellow_Potion_
		11566,10,	//Yellow_Potion_		
		11566,10,	//Yellow_Potion_
		11566,10,	//Yellow_Potion_
		11569,100,	//Orange_Potion_
		11569,100,	//Orange_Potion_
		11569,100,	//Orange_Potion_
		11569,100,	//Orange_Potion_
		11569,100,	//Orange_Potion_
		11569,100,	//Orange_Potion_
		11569,10,	//Orange_Potion_
		11569,10,	//Orange_Potion_
		11569,10,	//Orange_Potion_
		11569,10,	//Orange_Potion_
		11570,100,	//Red_Potion_
		11570,100,	//Red_Potion_
		11570,100,	//Red_Potion_
		11570,100,	//Red_Potion_
		11570,100,	//Red_Potion_
		11570,100,	//Red_Potion_
		11570,10,	//Red_Potion_
		11570,10,	//Red_Potion_
		11570,10,	//Red_Potion_
		11570,10,	//Red_Potion_
		11571,100,	//Green_Potion_
		11571,100,	//Green_Potion_
		11571,100,	//Green_Potion_
		11571,100,	//Green_Potion_
		11571,100,	//Green_Potion_
		11571,100,	//Green_Potion_
		11571,10,	//Green_Potion_
		11571,10,	//Green_Potion_
		11571,10,	//Green_Potion_
		11571,10,	//Green_Potion_
		11572,100,	//Blue_Potion_B
		11572,100,	//Blue_Potion_B
		11572,100,	//Blue_Potion_B
		11572,100,	//Blue_Potion_B
		11572,100,	//Blue_Potion_B
		11572,100,	//Blue_Potion_B
		11572,10,	//Blue_Potion_B
		11572,10,	//Blue_Potion_B
		11572,10,	//Blue_Potion_B
		11572,10,	//Blue_Potion_B
		18010,100,	//Rare_Coin
		18010,100,	//Rare_Coin
		18010,100,	//Rare_Coin
		18010,100,	//Rare_Coin
		18010,100,	//Rare_Coin
		18010,100,	//Rare_Coin
		18010,50,	//Rare_Coin
		18010,50,	//Rare_Coin
		18010,50,	//Rare_Coin
		18010,250,	//Rare_Coin
		18005,5,	//Mixed_Rune_Fragment
		18005,3,	//Mixed_Rune_Fragment
		18005,1,	//Mixed_Rune_Fragment
		.@frag,100,	//Day-Based Ore
		.@frag,100,	//Day-Based Ore
		.@frag,100,	//Day-Based Ore
		.@frag,100,	//Day-Based Ore
		.@frag,100,	//Day-Based Ore
		.@frag,100,	//Day-Based Ore
		.@frag,20,	//Day-Based Ore
		.@frag,20,	//Day-Based Ore
		.@frag,20,	//Day-Based Ore
		.@frag,20,	//Day-Based Ore
		.@frag,20,	//Day-Based Ore
		.@frag,20,	//Day-Based Ore
		.@frag,20;	//Day-Based Ore

	getmapxy('Map$,.@x,.@y,BL_NPC,instance_npcname(strnpcinfo(0)));
	
	specialeffect 10; //55462
	specialeffect 307; //Forest Light
	hideonnpc instance_npcname(strnpcinfo(0)); //55462
	
	.@limit = getarraysize(.@reward);
	for(.@i = 1; .@i<=.@limit; .@i+=2) {
		if(rand(100) < (.@reward[.@i+1] + .@bonus))
			makeitem .@reward[.@i],1,'Map$,.@x+rand(3)-1,.@y+rand(3)-1;
	}
	
	end;

OnInstanceInit:
	disablenpc instance_npcname(strnpcinfo(0)); //55462
	end;	
}

1@begi,184,196,4	script	Curious Adventurer::alapovivig	671,{
	mes "[Curious Adventurer]";
	mes "Thank you for your help!";
	mes "I can send you back if needed.";
	next;
	if(select("Stay:Leave")==1) close;
	// switch('Mode)	{
	// 	case 1: callfunc("DWM_Counter",1); break;
	// 	case 2: callfunc("DWM_Counter",2); break;
	// 	case 3: callfunc("DWM_Counter",3); break;
	// }
	warp "prt_fild05", 144, 238;
	close;

OnInstanceInit:
	disablenpc strnpcinfo(3);
	end;
}
