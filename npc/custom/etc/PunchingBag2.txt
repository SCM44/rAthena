//===== rAthena Script =============================================
//= Emperium Sandbag NPC
//===== By: ========================================================
//= Wardruna
//===== Current Version: ===========================================
//= 0.1
//===== Changelog: =================================================
//= 0.1 Initial commit
//= 0.2 Add MD_KNOCKBACK_IMMUNE
//===== Compatible With: ===========================================
//= rAthena Project
//===== Additional Comments: =======================================
//= When duplicating this NPC. Only use NUMBER after the # [Sixte]
//==================================================================
prontera,252,79,0	script	BarricadeSandbag#0	844,{
	end;
	
OnMyMobDead:
OnInit:
	getunitdata(getnpcid(0), .@d);
	monster strnpcinfo(4),.@d[UNPC_X],.@d[UNPC_Y],"Player Sandbag",22001,1,strnpcinfo(0)+"::OnMyMobDead";
	.@dupid = atoi(strnpcinfo(2));
	.gid[.@dupid] = $@mobid[0];
	.mhp[.@dupid] = strmobinfo(4,22001);
	setunitdata .gid[.@dupid],UMOB_MODE,.@d[UMOB_MODE]|0x4000000|0x0200000; // Set MD_STATUS_IMMUNE (0x4000000) because EDP/burn/%MHP based status can skew the DPS counter so much.
	initnpctimer;
	end;
	
OnTimer5000:
	.@dupid = atoi(strnpcinfo(2));
	if(unitexists(.gid[.@dupid])){
		getunitdata(.gid[.@dupid],.@d);
		.@diff = (.mhp[.@dupid] - .@d[UMOB_HP]);
		.@diffxd = ((.@diff*60)/100);
		.@diffn = ((.@diffxd*80)/100);
		if(.@diff > 0){
			npctalk "Player Sandbag : J'ai subi " + (.@diff) + " de dégats durant les 5 dernières secondes. " + (.@diffxd) + " Avec une réduction de 40% ( Thara Berret ) et " + (.@diffn) + " de dégats si le joueur a en plus 20% de reduction sur l'ellement de ton spell/atk.";
			setunitdata .gid[.@dupid],UMOB_HP,.mhp[.@dupid];
			specialeffect EF_HEAL;
		}
	}
	initnpctimer;
}

prontera,260,79,0	duplicate(BarricadeSandbag#0)	BarricadeSandbag#1	844
prontera,252,70,0	duplicate(BarricadeSandbag#0)	BarricadeSandbag#2	844
prontera,260,70,0	duplicate(BarricadeSandbag#0)	BarricadeSandbag#3	844
prontera,252,57,0	duplicate(BarricadeSandbag#0)	BarricadeSandbag#4	844
prontera,259,57,0	duplicate(BarricadeSandbag#0)	BarricadeSandbag#5	844