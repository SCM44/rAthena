//===== rAthena Script =======================================
//= Izlude Deepsea Instance (kRo Zero)
//============================================================


//Dead Man NPC

1@izu,235,206,3	script	#start_swDeep2	HIDDEN_WARP_NPC,3,3,{

OnTouch:
	if(is_party_leader() == false) {
		end;
	}
	disablenpc instance_npcname(strnpcinfo(0));
	donpcevent instance_npcname("#start_swDeep") + "::OnStart"; 
	end;

OnInstanceInit:
	'Geyser_1 = 0;
	'Geyser_2 = 0;
	'Geyser_3 = 0;
	'Geyser_4 = 0;
	'Boss = 0;
	'RageCount = 0;
	'Map$ = instance_mapname("1@izu");
	end;

}

1@izu,235,206,3	script	#start_swDeep	HIDDEN_WARP_NPC,{
	
OnStart:
	monster 'Map$,0,0,"--ja--",20082,25,instance_npcname("#start_swDeep") + "::OnMobDead";
	monster 'Map$,0,0,"--ja--",20083,25,instance_npcname("#start_swDeep") + "::OnMobDead";
	monster 'Map$,0,0,"--ja--",20084,25,instance_npcname("#start_swDeep") + "::OnMobDead";
	monster 'Map$,0,0,"--ja--",20085,25,instance_npcname("#start_swDeep") + "::OnMobDead";
	end;

OnMobDead:
	if('RageCount < 5) {
		'RageCount++;
	}
	.@RageCount = 'RageCount*200;
	instance_announce 0,"Deep sea creatures that witness the death of their allies increased their rage score to "+.@RageCount+".",bc_map,"0xFFFFFF",FW_BOLD;
	end;

OnBoss:
	instance_announce 0,"It looks like a huge deep sea creature has appeared around the entrance of the dungeon.",bc_map,"0xFFFFFF",FW_BOLD;	
	viewpoint instance_mapname("1@izu"),1,231,213,0,0x00FFFF;
	sleep 5000;
	killmonsterall 'Map$;
	instance_announce 0,"Deepsea Coelacanth absorbed all summoned creatures and heals itself.",bc_map,"0xFFFFFF",FW_BOLD;	
	
	//Initialize Array
	deletearray $@mobid[0],getarraysize($@mobid);
		
	if('Boss == 0) monster instance_mapname("1@izu"),231,213,"--ja--",20081,1,instance_npcname("#start_swDeep") + "::OnBossDead";
	'Boss = $@mobid[0];
	unittalk 'Boss,"Earthdwellers, before the might of the sea!!";
	
	//Apply Modifiers
	getunitdata 'Boss,.@boss_data;
	setunitdata 'Boss,UMOB_MAXHP,.@boss_data[UMOB_MAXHP];
	setunitdata 'Boss,UMOB_HP,.@boss_data[UMOB_HP];
	setunitdata 'Boss,UMOB_ATKMIN,.@boss_data[UMOB_ATKMIN];
	setunitdata 'Boss,UMOB_ATKMAX,.@boss_data[UMOB_ATKMAX];
	setunitdata 'Boss,UMOB_MATKMIN,.@boss_data[UMOB_MATKMIN];
	setunitdata 'Boss,UMOB_MATKMAX,.@boss_data[UMOB_MATKMAX];
	setunitdata 'Boss,UMOB_DEF,.@boss_data[UMOB_DEF];
	setunitdata 'Boss,UMOB_MDEF,.@boss_data[UMOB_MDEF];
	setunitdata 'Boss,UMOB_HIT,.@boss_data[UMOB_HIT];
	setunitdata 'Boss,UMOB_FLEE,.@boss_data[UMOB_FLEE];
	
	initnpctimer;
	end;
	
OnTimer5000:
	'Bones = 0;
	getunitdata 'Boss, .@arr;
	.@label$ = instance_npcname("#start_swDeep") + "::OnThorn";
	.@percent_hp = (.@arr[UMOB_HP] * 100) / .@arr[UMOB_MAXHP];
	.@mobx = .@arr[UMOB_X];
	.@moby = .@arr[UMOB_Y];

	// event type every 10%
	.@x1 = F_Rand(-7,-6,-5,-4,-3,3,4,5,6,7);
	.@y1 = F_Rand(-7,-6,-5,-4,-3,3,4,5,6,7);
	
	.@x2 = F_Rand(-7,-6,-5,-4,-3,3,4,5,6,7);
	.@y2 = F_Rand(-7,-6,-5,-4,-3,3,4,5,6,7);
	
	.@x3 = F_Rand(-7,-6,-5,-4,-3,3,4,5,6,7);
	.@y3 = F_Rand(-7,-6,-5,-4,-3,3,4,5,6,7);
	
	.@x4 = F_Rand(-7,-6,-5,-4,-3,3,4,5,6,7);
	.@y4 = F_Rand(-7,-6,-5,-4,-3,3,4,5,6,7);
	
	.@x5 = F_Rand(-7,-6,-5,-4,-3,3,4,5,6,7);
	.@y5 = F_Rand(-7,-6,-5,-4,-3,3,4,5,6,7);

	switch( .@percent_hp / 10 ) {
	case 10: 
	case 9:
	case 8:
		'Bones = 1;
		monster 'Map$[0],.@mobx+.@x1,.@moby+.@y1,"",1960,1, .@label$;
		break;	
	case 7:
	case 6:
	case 5:
		'Bones = 2;
		monster 'Map$[0],.@mobx+.@x1,.@moby+.@y1,"",1960,1, .@label$;
		monster 'Map$[0],.@mobx+.@x2,.@moby+.@y2,"",1960,1, .@label$;
		break;		
	case 4:
	case 3:	
		'Bones = 3;
		monster 'Map$[0],.@mobx+.@x1,.@moby+.@y1,"",1960,1, .@label$;
		monster 'Map$[0],.@mobx+.@x2,.@moby+.@y2,"",1960,1, .@label$;
		monster 'Map$[0],.@mobx+.@x3,.@moby+.@y2,"",1961,1, .@label$;
		break;	
	case 2:
	case 1:
	case 0:
		'Bones = 5;
		monster 'Map$[0],.@mobx+.@x1,.@moby+.@y1,"",1960,1, .@label$;
		monster 'Map$[0],.@mobx+.@x2,.@moby+.@y2,"",1960,1, .@label$;
		monster 'Map$[0],.@mobx+.@x3,.@moby+.@y3,"",1960,1, .@label$;
		monster 'Map$[0],.@mobx+.@x4,.@moby+.@y4,"",1960,1, .@label$;
		monster 'Map$[0],.@mobx+.@x5,.@moby+.@y5,"",1961,1, .@label$;
		break;	
	}
	end;
	
OnTimer65000:
	.@label$ = instance_npcname("#start_swDeep") + "::OnThorn";
	killmonster 'Map$[0], .@label$;
	instance_announce 0,"The Coelacanth has feasted on "+'Bones+" bones, recovering "+('Bones * 9999)+" HP in total.",bc_map,"0xFFFFFF",FW_BOLD;	
	getunitdata 'Boss, .@arr;
	setunitdata 'Boss,UMOB_HP,.@arr[UMOB_HP] + ('Bones * 9999);
	movenpc instance_npcname(strnpcinfo(0)),.@arr[UMOB_X],.@arr[UMOB_Y];
	specialeffect EF_HEAL,AREA,instance_npcname(strnpcinfo(0));
	initnpctimer;
	end;
			
OnThorn:
	end;
	
OnBossDead:
	stopnpctimer;
	.@label$ = instance_npcname("#start_swDeep") + "::OnThorn";
	killmonster 'Map$[0], .@label$;
	enablenpc instance_npcname("Treasure Chest#izu1");
	enablenpc instance_npcname("Memorial Society Member#izu3");
	end;
}


1@izu,249,193,3	script	Fisherman's Corpse#	4_M_DIEMAN,{

	mes "It seems he's been dead for a long time. Shall we open the journal that's lying next to him?";
	next;
	switch(select("Ignore.","Read it.","Cover with soil.")){
		
		case 1:
			mes "...";
			close;
		
		case 2:
			mes "[The Fisherman's Note]";
			mes "I was fascinated by the story of a fishing ground where no one came. As rumored, it was a place where seafood parties were held, but there is something strange about this place.";
			next;
			mes "[The Fisherman's Note]";
			mes "Until I caught the first few, I did not feel any discomfort. However, I soon realized. These guys seem to recognize the creatures around them as companions.";
			next;
			mes "[The Fisherman's Note]";
			mes "Whenever a comrade dies, they quietly get angry, and at some point they become violent out of control.";
			next;
			mes "[The Fisherman's Note]";
			mes "The entrance is right in front of me, but I can see a merman walking in front of me. It seems that they haven't found me hiding behind the rock yet.";
			next;
			mes "[The Fisherman's Note]";
			mes "As soon as he disappears, I have to run quickly and get out.";
			viewpoint 1,47,347,1,0xFF0000; //Poison1
			viewpoint 1,325,331,2,0xFF0000; //Poison2
			viewpoint 1,326,63,3,0xFF0000; //Poison3
			viewpoint 1,62,45,4,0xFF0000; //Poison4
			close;
		
		case 3:
			mes "...";
			close;
	
	}
}


1@izu,47,347,4	script	#btn_1	1_SHADOW_NPC,{

	if(is_party_leader() == false) {
		end;
	}
	if('RageCount < 5){
		mes "Need at least 1000 dungeon rage to activate the portal.";
		close;
	}
	'RageCount = 0;
	specialeffect EF_BIG_PORTAL;
	progressbar "ffff00",7;
	viewpoint 2,47,347,1,0xFF0000; //Poison1
	'Geyser_1 = 1;
	instance_announce 0,"Corrupted Seawater has been blocked from draining. It seems that some of the deep sea creatures that had gone berserk have subsided.",bc_map,"0xFFFFFF";
	.@sum = ('Geyser_1 + 'Geyser_2 + 'Geyser_3 + 'Geyser_4);
	if(.@sum == 4) donpcevent instance_npcname("#start_swDeep") + "::OnBoss";	
	hideonnpc instance_npcname(strnpcinfo(0));
	areamonster 'Map$,43,350,52,342,"--ja--",20085,10;
	end;
}

1@izu,325,331,4	script	#btn_2	1_SHADOW_NPC,{
	if(is_party_leader() == false) {
		end;
	}
	if('RageCount < 5){
		mes "Need at least 1000 dungeon rage to activate the portal.";
		close;
	}
	'RageCount = 0;
	specialeffect EF_BIG_PORTAL;
	progressbar "ffff00",7;
	viewpoint 2,325,331,2,0xFF0000; //Poison2
	'Geyser_2 = 1;
	instance_announce 0,"Corrupted Seawater has been blocked from draining. It seems that some of the deep sea creatures that had gone berserk have subsided.",bc_map,"0xFFFFFF";
	.@sum = ('Geyser_1 + 'Geyser_2 + 'Geyser_3 + 'Geyser_4);
	if(.@sum == 4) donpcevent instance_npcname("#start_swDeep") + "::OnBoss";	
	hideonnpc instance_npcname(strnpcinfo(0));
	areamonster 'Map$,320,335,331,327,"--ja--",20084,10;
	end;			
}

1@izu,326,63,4	script	#btn_3	1_SHADOW_NPC,{
	if(is_party_leader() == false) {
		end;
	}
	if('RageCount < 5){
		mes "Need at least 1000 dungeon rage to activate the portal.";
		close;
	}
	'RageCount = 0;
	specialeffect EF_BIG_PORTAL;
	progressbar "ffff00",7;
	viewpoint 2,326,63,3,0xFF0000; //Poison3
	'Geyser_3 = 1;
	instance_announce 0,"Corrupted Seawater has been blocked from draining. It seems that some of the deep sea creatures that had gone berserk have subsided.",bc_map,"0xFFFFFF";
	.@sum = ('Geyser_1 + 'Geyser_2 + 'Geyser_3 + 'Geyser_4);
	if(.@sum == 4) donpcevent instance_npcname("#start_swDeep") + "::OnBoss";	
	hideonnpc instance_npcname(strnpcinfo(0));
	areamonster 'Map$,324,68,331,61,"--ja--",20083,10;
	end;	
}

1@izu,62,45,4	script	#btn_4	1_SHADOW_NPC,{
	if(is_party_leader() == false) {
		end;
	}
	if('RageCount < 5){
		mes "Need at least 1000 dungeon rage to activate the portal.";
		close;
	}
	'RageCount = 0;
	specialeffect EF_BIG_PORTAL;
	progressbar "ffff00",7;
	viewpoint 2,62,45,4,0xFF0000; //Poison4
	'Geyser_4 = 1;
	instance_announce 0,"Corrupted Seawater has been blocked from draining. It seems that some of the deep sea creatures that had gone berserk have subsided.",bc_map,"0xFFFFFF";
	.@sum = ('Geyser_1 + 'Geyser_2 + 'Geyser_3 + 'Geyser_4);
	if(.@sum == 4) donpcevent instance_npcname("#start_swDeep") + "::OnBoss";	
	hideonnpc instance_npcname(strnpcinfo(0));
	areamonster 'Map$,58,48,65,42,"--ja--",20082,10;
	end;	
}


1@izu,236,214,4	script	Treasure Chest#izu1	4_TREASURE_BOX,{
		
		setarray .@reward[1],
		25424,100,
		25424,100,
		25424,100,
		25424,100,
		25424,100,
		25424,100,
		25424,70,
		25424,70,
		25424,40,
		25424,40,
		25476,100,
		25476,70,
		25476,70,
		25476,70,
		25476,40,
		25476,40,
		25476,20,
		25476,20,
		23649,100,
		23649,70,
		23649,40,
		25459,100,	//
		25459,50,
		18005,100,
		18005,50,
		18005,50,
		18005,20,
		18005,10,
		18005,10,
		18005,20,
		18005,10,
		18005,5,
		29586,30,
		29586,15,
		29586,7,
		29586,3,
		29586,1,
		29584,20,
		29584,10,
		29584,5,
		29584,5,	//
		29584,5,
		29589,100,
		29589,100,
		29589,50,
		29589,50,
		29589,20,
		29589,20,
		29589,10,
		29589,10,
		29589,5,
		29589,5,
		25459,10;	//

	getmapxy(.@map$,.@x,.@y,BL_NPC,instance_npcname(strnpcinfo(0)));
	
	specialeffect EF_ASSUMPTIO; 
	specialeffect EF_FORESTLIGHT2; //Forest Light
	specialeffect EF_COIN;
	specialeffect EF_STEAL;
	specialeffect EF_STEAL;
	specialeffect EF_STEAL;
	specialeffect EF_STEAL;
	specialeffect EF_STEAL;
	specialeffect EF_STEAL;
	specialeffect EF_STEAL;
	specialeffect EF_STEAL;
	specialeffect EF_STEAL;
	disablenpc instance_npcname(strnpcinfo(0)); //55462
	.@limit = getarraysize(.@reward);
	for(.@i = 1; .@i<=.@limit; .@i+=2) {
		if(rand(100) < (.@reward[.@i+1] + .@bonus))
			makeitem .@reward[.@i],1,'Map$,.@x+rand(3)-1,.@y+rand(3)-1;
	}
	end;
	
OnInstanceInit:
	disablenpc instance_npcname(strnpcinfo(0)); //55462
	end;	
}

1@izu,240,217,4	script	Memorial Society Member#izu3	702,{
	mes "["+strnpcinfo(1)+"]";
	mes "It's time to get out of here, the dimensional rift is getting weak!";
	next;
	switch(select("Wait, a little bit!","Let's get moving!")){
		
		case 1:
			mes "["+strnpcinfo(1)+"]";
			mes "Hurry up! We might be stuck here forever if we miss the portal!";
			close;
		
		case 2:
			mes "["+strnpcinfo(1)+"]";
			mes "Sending you outside now...";
			close2;
			//getitem 25424, F_Rand(3,4,5);
			warp "prt_fild06",37,192;
			end;
	}
	

OnInstanceInit:
	disablenpc instance_npcname(strnpcinfo(0)); //55462
	end;	
}
